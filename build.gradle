plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.121'
    id 'idea'
}


version = mod_version
group = mod_group_id

repositories {
    maven { url "https://maven.iglee.fr/releases" }
    maven { url "https://modmaven.dev/" }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {

    version = project.neo_version

    runs {
        client {
            client()
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {

    implementation "mekanism:Mekanism:${minecraft_version}-${mekanism_version}"

    implementation "fr.iglee42:IgleeLib:${minecraft_version}-${lib_version}"
    implementation "fr.iglee42:EvolvedMekanism:${minecraft_version}-${evolved_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            mekanism_version_range : mekanism_version_range,
            evolved_version_range  : evolved_version_range
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
